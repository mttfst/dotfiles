set path+=**
set wildmenu

" Tab options
set tabstop=4 softtabstop=4
set shiftwidth=4
set expandtab
set smartindent

" Line numbers
set number relativenumber

" source prject vimrc's
set exrc

" search results don't stay hightlighted
set nohlsearch

" keep buffers open
set hidden

set noerrorbells

" case sensitive search when a cap letter is used
set smartcase

" history
set noswapfile
set nobackup
set undodir=~/.vim/undodir
set undofile

" highlight on searching
set incsearch

" start scroll before last line
set scrolloff=8

" extra column on the left for signals from whatever
set signcolumn=yes

" Declare the list of plugins.
call plug#begin('~/.vim/plugged')

Plug 'lervag/vimtex'
Plug 'mbbill/undotree'

Plug 'ajh17/VimCompletesMe'

call plug#end()


" remaps
let mapleader = " "

"navigation between screens
map <leader>h :wincmd h<CR>
map <leader>j :wincmd j<CR>
map <leader>k :wincmd k<CR>
map <leader>l :wincmd l<CR>
" resize window"
nnoremap <Leader>+ :vertical resize +5<CR>
nnoremap <Leader>- :vertical resize -5<CR>

" move split windows"
nnoremap <Leader>wh <C-w>H
nnoremap <Leader>wj <C-w>J
nnoremap <Leader>wk <C-w>K
nnoremap <Leader>wl <C-w>L

" move lines up and down in visual"
vnoremap J :m '>+1<CR>gv=gv
vnoremap K :m '<-2<CR>gv=gv

" In insert or command mode, move normally by using Ctrl
inoremap <C-h> <Left>
inoremap <C-j> <Down>
inoremap <C-k> <Up>
inoremap <C-l> <Right>

" undotree settings"
nnoremap <leader>u :UndotreeToggle<CR>


" VimTex settings"
nnoremap <leader>toc :VimtexTocToggle <CR> :wincmd h <CR>
nnoremap <leader>tv :VimtexView <CR>
nnoremap <leader>tc :VimtexCompile <CR>
let g:vimtex_view_method = 'skim'
let g:vimtex_compiler_progname='nvr'
let g:vimtex_grammar_textidote = {
            \ 'jar': '~/.vim/textiode/textidote.jar',
            \ 'args': '--output singleline --check en_UK',
            \}


" source vimrc"
nnoremap <leader>svrc :source ~/.config/nvim/init.vim <CR>


" toggle to absolute numbers in insert mode
augroup numbertoggle
  autocmd!
  autocmd BufEnter,FocusGained,InsertLeave * set relativenumber
  autocmd BufLeave,FocusLost,InsertEnter   * set norelativenumber
augroup END

" remove white spaces
fun! TrimWhitespace()
  let l:save = winsaveview()
  keeppatterns %s/\s\+$//e
  call winrestview(l:save)
endfun

augroup whitespace
  autocmd!
  autocmd BufWritePre * :call TrimWhitespace()
augroup END


" highlight yanked lines a seconds"
augroup highlight_yank
    autocmd!
    autocmd TextYankPost * silent! lua require'vim.highlight'.on_yank({timeout = 1000})
augroup END



function! SetServerName()
  if has('win32')
    let nvim_server_file = $TEMP . "/curnvimserver.txt"
  else
    let nvim_server_file = "/tmp/curnvimserver.txt"
  endif
  let cmd = printf("echo %s > %s", v:servername, nvim_server_file)
  call system(cmd)
endfunction

augroup vimtex_common
    autocmd!
    autocmd FileType tex call SetServerName()
augroup END


augroup VimCompletesMeTex
  autocmd!
  autocmd FileType tex
      \ let b:vcm_omni_pattern = g:vimtex#re#neocomplete
augroup END

" no new line when hit enter in completion list"
inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"
